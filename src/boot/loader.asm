org 0x100


; ======================================================================================================================
; ----------------------------------------------------------------------------------------------------------------------
; 一些宏定义，汇编伪指令不会产生机器代码
; ----------------------------------------------------------------------------------------------------------------------
StackBase       equ     0x100       ; 用0x7c00以下的地方来作为栈，要注意不要达到0x0500
MessageLength	equ 	10          ; 为简化代码, 下面每个要打印的字符串的长度均为 MessageLength
; ======================================================================================================================


    jmp START


; ======================================================================================================================
; ----------------------------------------------------------------------------------------------------------------------
;   头文件
; ----------------------------------------------------------------------------------------------------------------------
%include "load.inc"         ; 挂载点相关的信息
%include "fat12hdr.inc"     ; 导入它是因为需要这些信息来加载内核文件
%include "pm.inc"           ; 保护模式的一些信息，各种宏和变量
; ======================================================================================================================


; ======================================================================================================================
; ----------------------------------------------------------------------------------------------------------------------
;   GDT全局描述符表相关信息以及堆栈信息
; ----------------------------------------------------------------------------------------------------------------------
; 描述符                        基地址        段界限       段属性
GDT			        Descriptor	0,          0,          0							; 空描述符，必须存在，不然CPU无法识别GDT
CodeSegDesc         Descriptor	0,          0xfffff,    DA_32 | DA_CR | DA_LIMIT_4K	; 0~4G，32位可读代码段，粒度为4KB
DataSegDesc         Descriptor  0,          0xfffff,    DA_32 | DA_DRW | DA_LIMIT_4K; 0~4G，32位可读写数据段，粒度为4KB
VideoSegDesc        Descriptor  0xb8000,    0xfffff,    DA_DRW | DA_DPL3            ; 视频段，特权级3（用户特权级）

; GDT全局描述符表
GDTLen              equ     $ - GDT                         ; GDT的长度
gdtPtr              dw      GDTLen - 1                      ; GDT指针.段界限
                    dd      LoaderPhyAddr + GDT           ; GDT指针.基地址
; GDT选择子
SelectorCode        equ     CodeSegDesc - GDT               ; 代码段选择子
SelectorData        equ     DataSegDesc - GDT               ; 数据段选择子
SelectorVideo       equ     VideoSegDesc - GDT | SA_RPL3    ; 视频段选择子，特权级3（用户特权级）
; ======================================================================================================================


; ======================================================================================================================
; ----------------------------------------------------------------------------------------------------------------------
;   16位实模式代码段
; ----------------------------------------------------------------------------------------------------------------------
START:
    ; 寄存器复位
    mov	ax, cs              ; 此时的cs为LoaderSeg，即0x9000，ip为0x0100
    mov	ds, ax
    mov	es, ax
    mov	ss, ax
    mov	sp, StackBase       ; sp指向当前栈顶，栈向低地址生长

    ; 显示字符串 "Loading..."
    mov	dh, 0
    call PRINT_STRING       ; 显示字符串

   ; 得到内存数
    mov ebx, 0              ; ebx = 后续值，开始时需为0
    mov di, memChkBuf16      ; es:di 指向一个地址范围描述符结构(Address Range Descriptor Structure)
CHECK_MEM:
	mov eax, 0E820h		    ; eax = 0000E820h
	mov ecx, 20			    ; ecx = 地址范围描述符结构的大小
	mov edx, 0534D4150h	    ; edx = 'SMAP'
	int 15h
	jc CHECK_MEM_FAIL		    ; 如果产生的进位，即CF = 1，跳转到.MemChkFail
	add di, 20
	inc dword [mcrCountDD16]	; _dwMCRNumber = ARDS　的个数
	cmp ebx, 0
	jne CHECK_MEM		    ; ebx != 0，继续进行循环
	jmp CHECK_MEM_OK		    ; ebx == 0，得到内存数OK
CHECK_MEM_FAIL:
	mov dword [mcrCountDD16], 0
CHECK_MEM_OK:
    ; 操作软盘前，现将软驱复位
    xor ah, ah              ; xor:异或，ah = 0
    xor dl, dl              ; dl = 0
    int 0x13
    ; 接下来我们在软盘A中开始寻找文件
    mov word [sectorW], SectorNoOfRootDirectory     ; 读取软盘的根目录扇区号
SEARCH_FILE_IN_ROOR_DIR_BEGIN:
    cmp word [rootDirSizeLoopW], 0
    jz NO_FILE                  ; 读完了整个根目录扇区都没找到，所以没有
    dec word [rootDirSizeLoopW] ; wRootDirSizeLoop--

    ; 读取扇区
    mov ax, KernelSeg          ; es = KERNEL_SEG
    mov es, ax
    mov bx, KernelOffset
    mov ax, [sectorW]
    mov cl, 1
    call READ_SECTOR

    mov si, kernelFileName      ; ds:si -> Loader的文件名称
    mov di, KernelOffset       ; es:di -> KERNEL_SEG:KERNEL_OFFSET -> 加载到内存中的扇区数据
    cld                         ; 字符串比较方向，si、di方向向右

    ; 开始在扇区中寻找文件，比较文件名
    mov dx, 16                  ; 一个扇区512字节，FAT目录项占用32个字节，512/32 = 16，所以一个扇区有16个目录项
SEARCH_FOR_FILE:
    cmp dx, 0
    jz NEXT_SECTOR_IN_ROOT_DIR  ; 读完整个扇区，依旧没找到，准备加载下一个扇区
    dec dx                      ; dx--
    ; 应该开始比较目录项中的文件名了
    mov cx, 11
CMP_FILENAME:
    cmp cx, 0
    jz FILENAME_FOUND           ; cx = 0，整个文件名里的字符都匹配上了，我们发现它了
    dec cx                      ; cx--
    lodsb                       ; ds:si -> al, si++
    cmp al, byte [es:di]        ; 比较字符
    je GO_ON                    ; 字符相同，准备继续比较下一个
    jmp DIFFERENT               ; 只要有一个字符不相同，就表明本目录项不是我们要寻找的文件的目录项

GO_ON:
    inc di
    jmp  CMP_FILENAME
DIFFERENT:
    and di, 0xfff0              ; di &= f0, 11111111 11110000，是为了让它指向本目录项条目的开始。

    add di, 32                  ; di += 32， 让di指向下一个目录项
    mov si, kernelFileName
    jmp SEARCH_FOR_FILE         ; 重新开始在下一个目录项中查找文件并比较

NEXT_SECTOR_IN_ROOT_DIR:
    add word [sectorW], 1       ; 准备开始读取下一个扇区
    jmp SEARCH_FILE_IN_ROOR_DIR_BEGIN

NO_FILE:
    mov dh, 4
    call PRINT_STRING                ; 打印"NO KERNEL!"
    ; 死循环
    jmp $
FILENAME_FOUND:
    ; 准备参数，开始读取文件数据扇区
    mov ax, RootDirSectors      ; ax = 根目录占用空间（占用的扇区数）
    and di, 0xfff0              ; di &= f0, 11111111 11110000，是为了让它指向本目录项条目的开始。

    push eax                    ; 保存eax的值
    mov eax, [es:di + 0x1c]     ; FAT目录项第0x1c处偏移是文件大小
    mov dword [kernelSizeDW], eax   ; 保存内核文件大小
    cmp eax, KernelHaveSpace  ; 看看内核文件大小有没有超过我们为其保留的大小
    ja KERNEL_FILE_TOO_LARGE    ; 超过了！
    pop eax                     ; 恢复eax
    jmp FILE_START_LAOD         ; 没超过，准备开始加载内核文件
KERNEL_FILE_TOO_LARGE:          ; 内核文件太大了，超过了我们给它留的128KB
    mov dh, 3
    call PRINT_STRING                ; 打印"Too Large!"
    jmp $                       ;
FILE_START_LAOD:
    add di, 0x1a                ; FAT目录项第0x1a处偏移是文件数据所在的第一个簇号
    mov cx, word [es:di]        ; cx = 文件数据所在的第一个簇号
    push cx                     ; 保存文件数据所在的第一个簇号
    ; 通过簇号计算它的真正扇区号
    add cx, ax
    add cx, DeltaSectorNo       ; 簇号 + 根目录占用空间 + 文件开始扇区号 == 文件数据的第一个扇区
    mov ax, KernelSeg
    mov es, ax                  ; es <- KERNEL_SEG
    mov bx, KernelOffset       ; bx <- KERNEL_OFFSET
    mov ax, cx                  ; ax = 文件数据的第一个扇区
LOADING_FILE:
    ; 我们每读取一个数据扇区，就在“Loading...”之后接着打印一个点，形成一种动态加载的动画。
    ; 0x10中断，0xe功能 --> 在光标后打印一个字符
    push ax
    push bx
    mov ah, 0xe
    mov al, '.'
    mov bl, 0xf
    int 0x10
    pop bx
    pop ax

    mov cl, 1                   ; 读1个
    call READ_SECTOR             ; 读取
    pop ax                      ; 取出前面保存的文件的的簇号
    call GET_FATEntry           ; 通过簇号获得该文件的下一个FAT项的值
    cmp ax, 0xff8
    jae FILE_LOADED             ; 加载完成...
    ; FAT项的值 < 0xff8，那么我们继续设置下一次要读取的扇区的参数
    ; 通过簇号计算它的真正扇区号
    push ax                     ; 保存簇号
    mov dx, RootDirSectors
    add ax, dx
    add ax, DeltaSectorNo       ; 簇号 + 根目录占用空间 + 文件开始扇区号 == 文件数据的扇区
    add bx, [bpbBytsPerSec]    ; bx += 扇区字节量
    jc KERNEL_GREAT_64KB        ; 如果bx += 扇区字节量，产生了一个进位，说明已经读满64KB，内核文件大于64KB
    jmp CONTINUE_LOADING        ; 内核文件还在64KB内，继续正常加载
KERNEL_GREAT_64KB:
    ; es += 0x1000，es指向下一个段，准备继续加载
    push ax
    mov ax, es
    add ax, 0x1000
    mov es, ax
    pop ax
CONTINUE_LOADING:               ; 继续加载内核文件
    jmp LOADING_FILE
FILE_LOADED:
    call KillMotor              ; 关闭软驱马达

    mov dh, 1
    call PRINT_STRING                ; 打印"KERNEL OK!"
;----------------------------------------------------------------------------
; 万事俱备，准备进入32位保护模式
;----------------------------------------------------------------------------
    ; 1 首先，进入保护模式必须有 GDT 全局描述符表，我们加载 gdtr（gdt地址指针）
    lgdt	[gdtPtr]

    ; 2 由于保护模式中断处理的方式和实模式不一样，所以我们需要先关闭中断，否则会引发错误
    cli

    ; 3 打开地址线A20，不打开也可以进入保护模式，但内存寻址能力受限（1MB）
    in al, 92h
    or al, 00000010b
    out 92h, al

    ; 4 进入16位保护模式，设置cr0的第0位：PE（保护模式标志）为1
    mov eax, cr0
    or 	eax, 1
    mov cr0, eax

; ----------------------------------------------------------------------------------------------------------------------
; 注意，我们并没有去设置GDT中代码段描述符的32位段基地址，它仍然是0，但是我们可以利用偏移量来进入32保护模式，去执行PM_32_START处的代码。
; 这里在补充一下32位保护模式下的内存寻址的计算方式。
; 此时，下一条指令地址是这样计算的，cs存着的是段选择子，gdtr存着的是gdt的线性基地址和界限，cpu可以通过此二者来访问到gdt的某一项，
; 然后取出该项中的段线性基地址，最后加上ip中的偏移量，这样就可以得知下一条指令的物理地址了。
; ----------------------------------------------------------------------------------------------------------------------

    ; 5 真正进32位入保护模式！前面的4步已经进入了保护模式
    ; 	现在只需要跳入到一个32位代码段就可以真正进入32位保护模式了！
    jmp dword SelectorCode:LoaderPhyAddr + PM_32_START

    ; 如果上面一切顺利，这一行永远不可能执行的到
    jmp $

; ======================================================================================================================
; ----------------------------------------------------------------------------------------------------------------------
; 一些变量
; ----------------------------------------------------------------------------------------------------------------------
rootDirSizeLoopW    dw      RootDirSectors  ; 根目录占用的扇区数14，在循环中将被被逐步递减至0
sectorW             dw      0               ; 要读取的扇区号
isOdd               db      0               ; 读取的FAT条目是不是奇数项?
kernelSizeDW        dd      0               ; 内核文件的大小
; ----------------------------------------------------------------------------------------------------------------------
; 要显示的字符串
; db 汇编伪指令，表示其后的每个操作数占1字节。dw，2字节。dd，double word 4字节。
; ----------------------------------------------------------------------------------------------------------------------
kernelFileName		db  "KERNEL  BIN", 0	; KERNEL.BIN 之文件名

loaderMessage 		db	"Loading..."        ; 10, 不够则用空格补齐. 序号 0
                    db  "KERNEL OK!"        ; 序号1
                    db  "MemChkFail"        ; 序号2
                    db  "Too large!"        ; 序号3
                    db  "NO KERNEL!"        ; 序号4
                    db  "BreakPoint"        ; 序号5
; ======================================================================================================================


; ======================================================================================================================
; ----------------------------------------------------------------------------------------------------------------------
;   16位支持函数库
; ----------------------------------------------------------------------------------------------------------------------
%include "loader_16lib.inc"
; ======================================================================================================================


; ======================================================================================================================
; ----------------------------------------------------------------------------------------------------------------------
;   32位代码段
; ----------------------------------------------------------------------------------------------------------------------
[section .code32]
align 32
[bits 32]
PM_32_START:            ; 跳转到这里，说明已经进入32位保护模式
    mov ax, SelectorData
    mov ds, ax
    mov es, ax
    mov fs, ax
    mov ss, ax              ; ds = es = fs = ss = 数据段
    mov esp, StackTop     ; 设置栈顶
    mov ax, SelectorVideo
    mov gs, ax              ; gs = 视频段

    ; 计算内存大小
    call CalcMemSize

    ; 打印内存信息
    call PrintMemSize
    ; 启动分页机制
    call SetupPaging
    ; 初始化内核程序
    call InitKernelFile

    ; 进入内核前，我们别忘了将一些重要的参数保存，以便以后内核可以很方便的获取它们
    mov dword [BootParamAddr], BootParamMagic   ; Flyanx引导参数的魔数
    mov eax, [memSizeDD16]                            ; 内存大小
    mov [BootParamAddr + 4], eax                  ; 第一个引导参数：内存大小
    mov eax, KernelPhyAddr
    add eax, KernelOffset                          ; 内核文件的物理地址 =
                                                    ; KERNEL_SEG * 16 + KERNEL_OFFSET
    mov [BootParamAddr + 8], eax                  ; 第二个引导参数：内核文件所在物理地址
    ; 暂时我只想到这两个参数需要保存，以后有更多参数可以直接追加保存在后面，我们保留给引导参数的内存还挺大的！

    ; ***********************************************************************
    ; 正式跳入内核，Loader将CPU控制权转交给内核，至此，Loader的使命也结束了！比Boot厉害吧！
    ; 从这里的函数运行成功后，我们才真正算是进入编写操作系统的门槛！
    jmp SelectorCode:KernelEntryPointPhyAddr

    ; 永远到达不了的真实
    jmp $
; ======================================================================================================================


; ======================================================================================================================
; ----------------------------------------------------------------------------------------------------------------------
;   32位支持函数库
; ----------------------------------------------------------------------------------------------------------------------
%include "loader_32lib.inc"
; ======================================================================================================================


; ======================================================================================================================
; ----------------------------------------------------------------------------------------------------------------------
; 32位数据段
; ----------------------------------------------------------------------------------------------------------------------
[section .data32]
    align 32                ; 32字节对齐
DATA32:

; ----------------------------------------------------------------------------------------------------------------------
; 16位实模式下使用的数据地址，变量以下划线作前缀与32位下的变量区分开。
; ----------------------------------------------------------------------------------------------------------------------
; 字符串
strMemSize16         db "Memory Size: ", 0
strKB16              db "KB", 10, 0
strSetupPaging16     db "Setup paging.", 10, 0
strDebug16           db "BreakPoint", 10,0

; 变量
mcrCountDD16         dd 0        ; 检查完成的ARDS的数量，为0则代表检查失败
memSizeDD16          dd 0        ; 内存大小
dispPositionDD16     dd (80 * 4 + 0) * 2 ; 初始化显示位置为第 4 行第 0 列

; 地址范围描述符结构(Address Range Descriptor Structure)，20字节
ARDS16:
    baseAddrLowDD16  dd 0        ; 基地址低32位
    baseAddrHighDD16 dd 0        ; 基地址高32位
    lengthLowDD16    dd 0        ; 内存长度（字节）低32位
    lengthHighDD16   dd 0        ; 内存长度（字节）高32位
    typeDD16         dd 0        ; ARDS的类型，用于判断是否可以被OS使用

; 内存检查结果缓冲区，用于存放没存检查的ARDS结构，256字节是为了对齐32位，256/20=12.8，所以这个缓冲区可以存放12个ARDS。
memChkBuf16          times 256 db 0

; ----------------------------------------------------------------------------------------------------------------------
; 32位保护模式下的数据地址符号，这里是16位下访问不到的
; ----------------------------------------------------------------------------------------------------------------------
StrMemSize32          equ LoaderPhyAddr + strMemSize16
StrKB32               equ LoaderPhyAddr + strKB16
StrSetupPaging32               equ LoaderPhyAddr + strSetupPaging16

MCRCountDD32          equ LoaderPhyAddr + mcrCountDD16
MemSizeDD32           equ LoaderPhyAddr + memSizeDD16
DispPositionDD32      equ LoaderPhyAddr + dispPositionDD16
ARDS32                equ LoaderPhyAddr + ARDS16
    BaseAddrLowDD32   equ LoaderPhyAddr + baseAddrLowDD16
    BaseAddrHighDD32  equ LoaderPhyAddr + baseAddrHighDD16
    LengthLowDD32     equ LoaderPhyAddr + lengthLowDD16
    LengthHighDD32    equ LoaderPhyAddr + lengthHighDD16
    TypeDD32          equ LoaderPhyAddr + typeDD16
MemChkBuf32           equ LoaderPhyAddr + memChkBuf16
; 堆栈就在数据段的末尾，一共给这个32位代码段堆栈分配4KB
StackSpace times 0x1000    db 0
StackTop  equ LoaderPhyAddr + $     ; 栈顶
; ======================================================================================================================
